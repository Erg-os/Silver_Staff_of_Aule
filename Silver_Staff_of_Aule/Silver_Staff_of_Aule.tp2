BACKUP ~Silver_Staff_of_Aule/backup~
AUTHOR ~Erg, at https://forums.beamdog.com~
VERSION ~v9.1~

ALWAYS
  // Macros to handle multiple tra encodings (slightly adapted from Wisp's original code)
  DEFINE_ACTION_MACRO bgee_language BEGIN
    ACTION_IF GAME_IS ~bgee~ BEGIN
      LOAD_TRA ~%MOD_FOLDER%/tra/bgee/english/game.tra~
      LOAD_TRA ~%MOD_FOLDER%/tra/bgee/%LANGUAGE%/game.tra~
    END
  END
  LAM bgee_language
END

LANGUAGE ~English~
         ~english~
         ~Silver_Staff_of_Aule/tra/english/game.tra~
         ~Silver_Staff_of_Aule/tra/english/setup.tra~
LANGUAGE ~Italian~
         ~italian~
         ~Silver_Staff_of_Aule/tra/english/game.tra~
         ~Silver_Staff_of_Aule/tra/english/setup.tra~
         ~Silver_Staff_of_Aule/tra/italian/game.tra~
         ~Silver_Staff_of_Aule/tra/italian/setup.tra~
LANGUAGE ~Polish~
         ~polish~
         ~Silver_Staff_of_Aule/tra/english/game.tra~
         ~Silver_Staff_of_Aule/tra/english/setup.tra~
         ~Silver_Staff_of_Aule/tra/polish/game.tra~
         ~Silver_Staff_of_Aule/tra/polish/setup.tra~

BEGIN @101
REQUIRE_PREDICATE GAME_IS ~bgee totsc tutu_totsc bgt~ @106

// Strings from TRA File
OUTER_SPRINT er_tra1 @0
OUTER_SPRINT er_tra2 @1
OUTER_SPRINT er_str0 @2
OUTER_SPRINT er_tra3 @3
OUTER_SPRINT er_tra4 @4
OUTER_SPRINT er_tra5 @5
OUTER_SPRINT er_tra6 @6
OUTER_SPRINT er_tra7 @7
OUTER_SPRINT er_tra8 @8
OUTER_SPRINT er_tra9 @9
OUTER_SPRINT er_tra10 @10

// Add Underscore Prefix for Tutu
ACTION_IF GAME_IS ~tutu_totsc~ THEN BEGIN
  PRINT @108
  OUTER_SPRINT er_pf ~_~
END ELSE BEGIN
  OUTER_SPRINT er_pf ~~
END

// Detect method used for Damage Bonus vs. Lycanthropes
COPY_EXISTING ~FT1DAM.EFF~ ~override~
  SET er_eff = 0
  READ_BYTE 0x10 er_dbl
  PATCH_IF er_dbl = 0x0c BEGIN
    PATCH_PRINT @113
    SET er_eff = 2
  END 
  PATCH_IF er_dbl = 0xb3 BEGIN
    PATCH_PRINT @112
    SET er_eff = 1
  END 
BUT_ONLY_IF_IT_CHANGES

ACTION_IF er_eff = 2 THEN BEGIN 
  COPY ~Silver_Staff_of_Aule\stuff\ER_STDM.EFF~ ~override~
END

// Detect if opcode 344 exists
COPY_EXISTING ~SW1H18.ITM~ ~override~ // Sword of Balduran
  SET er_op344 = 0
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    GET_OFFSET_ARRAY er_ge_array ITM_V10_GEN_EFFECTS
    PHP_EACH er_ge_array AS er_int => er_ge_off BEGIN
      READ_SHORT er_ge_off er_ge_type
      PATCH_IF er_ge_type = 344 BEGIN
        SET er_op344 = 1
      END
    END
  END
BUT_ONLY_IF_IT_CHANGES

ACTION_IF GAME_IS ~bgee totsc tutu_totsc~ THEN BEGIN 

  // Check for BGEE
  ACTION_IF GAME_IS ~bgee~ THEN BEGIN
    PRINT @110
  END

  // Check for Vanilla TotSC
  ACTION_IF GAME_IS ~totsc~ THEN BEGIN
    PRINT @109
  END

  COPY ~Silver_Staff_of_Aule\stuff\ER_IST2.BAM~ ~override/ER_IST1.BAM~

  COPY_EXISTING ~%er_pf%STAF08.ITM~ ~override~ // Aule's Staff
    PATCH_IF SOURCE_SIZE > 0x71 BEGIN
      READ_BYTE 0x19 er_flg // Read Flags (2nd byte only)
      WRITE_BYTE 0x19 (er_flg BOR 0b00000011) // Add Silver & Cold Iron Flags
      WRITE_LONG 0x34 7500 // New Price

      // Add +1 Damage Bonus vs. Lycanthropes
      PATCH_IF er_eff = 1 BEGIN
        LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EQEFFECT~
          INT_VAR
            opcode = 177               // Type: #177 (Use EFF File)
            target = 1                 // Target: Self
            parameter2 = 2             // IDS Target: EA.IDS
            timing = 2                 // Timing Mode: Instant/While Equipped
            probability1 = 100         // Probability1: 100
          STR_VAR resource = ~FT1DAM~  // Resource: FT1DAM
        END
      END
      PATCH_IF er_eff = 2 BEGIN
        LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~
          INT_VAR
            type = 1                    // Melee
            opcode = 177                // Type: #177 (Use EFF File)
            target = 2                  // Target: Preset Target
            parameter1 = 122            // IDS Value: LYCANTHROPE
            parameter2 = 4              // IDS Target: RACE.IDS
            timing = 1                  // Timing Mode: Instant/Permanent Until Death
            probability1 = 100          // Probability1: 100
            insert_point = 0            // First Effect
          STR_VAR resource = ~ER_STDM~  // Resource: ER_STDM
        END
      END

      // Add +1 THAC0 Bonus vs. Lycanthropes
      LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EQEFFECT~
        INT_VAR
          opcode = 177               // Type: #177 (Use EFF File)
          target = 1                 // Target: Self
          parameter2 = 2             // IDS Target: EA.IDS
          timing = 2                 // Timing Mode: Instant/While Equipped
          probability1 = 100         // Probability1: 100
        STR_VAR resource = ~FT1HIT~  // Resource: FT1HIT
      END

      // Add +1 Enchantment vs. Lycanthropes (BGEE v2+ only)
      PATCH_IF er_op344 BEGIN
        LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EQEFFECT~
          INT_VAR
            opcode = 344        // Type: #344 (Enchantment vs. Creature Type)
            target = 1          // Target: Self
            parameter1 = 122    // IDS Value: LYCANTHROPE
            parameter2 = 4      // IDS Target: RACE.IDS
            timing = 2          // Timing Mode: Instant/While Equipped
            probability1 = 100  // Probability1: 100
            special = 4         // Enchantment: 4
        END
      END

      // Make the Staff Look More Like Silver
      WRITE_ASCII 0x3a ~ER_IST1~ #8
      READ_LONG 0x64 er_ability_offset
      WRITE_ASCII %er_ability_offset%+4 ~ER_IST1~ #8
      LPF DELETE_ITEM_EQEFFECT
        INT_VAR
          opcode_to_delete = 7
      END
      LPF DELETE_ITEM_EQEFFECT
        INT_VAR
          opcode_to_delete = 8
      END
      LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EQEFFECT~
        INT_VAR
          opcode = 7          // Type: #7 (Set Character Colours by Palette)
          target = 1          // Target: Self
          parameter1 = 27     // Param1: Gradient Number
          parameter2 = 21     // Param2: Location
          timing = 2          // Timing Mode: Instant/While Equipped
          probability1 = 100  // Probability1: 100
      END
      LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EQEFFECT~
        INT_VAR
          opcode = 7          // Type: #7 (Set Character Colours by Palette)
          target = 1          // Target: Self
          parameter1 = 27     // Param1: Gradient Number
          parameter2 = 20     // Param2: Location
          timing = 2          // Timing Mode: Instant/While Equipped
          probability1 = 100  // Probability1: 100
      END
      LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EQEFFECT~
        INT_VAR
          opcode = 7          // Type: #7 (Set Character Colours by Palette)
          target = 1          // Target: Self
          parameter1 = 27     // Param1: Gradient Number
          parameter2 = 16     // Param2: Location
          timing = 2          // Timing Mode: Instant/While Equipped
          probability1 = 100  // Probability1: 100
      END

      SAY NAME2 @11

      READ_STRREF IDENTIFIED_DESC er_str1 // Read Identified Description

      // Read Some of the Staff Properties
      GET_OFFSET_ARRAY er_abar 0x64 4 0x68 2 0 0 0x38
      PHP_EACH er_abar AS er_nab => er_abof BEGIN
        READ_BYTE er_abof er_type 
        PATCH_IF er_type = 1 BEGIN
          READ_SHORT er_abof+0x14 er_thac0
          READ_BYTE er_abof+0x18 er_dam1
          READ_BYTE er_abof+0x16 er_dam2
          READ_SHORT er_abof+0x1a er_bdam
          READ_SHORT er_abof+0x1c er_dtype

          //PATCH_PRINT ~THAC0:%er_thac0%~
          //PATCH_PRINT ~DAMAGE:%er_dam1%D%er_dam2%+%er_bdam% (%er_dtype%)~

        END
      END

      // Replace Relevant Portions of the Identified Description
      INNER_PATCH_SAVE er_str1 ~%er_str1%~ BEGIN
        REPLACE_TEXTUALLY ~%er_tra1%~ ~%er_tra2%~
        REPLACE_TEXTUALLY ~%er_tra3%~ ~~

        //REPLACE_TEXTUALLY ~THAC[0O]:.+[%LNL%%MNL%%WNL%]~ ~~
        //REPLACE_TEXTUALLY ~Damage:.+[%LNL%%MNL%%WNL%]~ ~~

        REPLACE_TEXTUALLY ~%er_tra4%~ ~%er_tra5%~
        REPLACE_TEXTUALLY ~%er_tra6%~ ~%er_tra7%~
      END

      PATCH_IF ~%er_str1%~ STRING_CONTAINS_REGEXP ~%er_tra8%~ ||
               ~%er_str1%~ STRING_CONTAINS_REGEXP ~%er_tra9%~ ||
               ~%er_str1%~ STRING_CONTAINS_REGEXP ~%er_tra10%~ BEGIN
        PATCH_PRINT @115
        SAY_EVALUATED IDENTIFIED_DESC ~%er_str0%~ // Full Description
      END ELSE BEGIN
        PATCH_PRINT @114
        SAY_EVALUATED IDENTIFIED_DESC ~%er_str1%~ // Patched Description
      END

    END
  BUT_ONLY_IF_IT_CHANGES
END ELSE BEGIN

  // Add Staff for BGT
  PRINT @111

  COPY ~Silver_Staff_of_Aule\stuff\ER_IST2.BAM~ ~override/ER_IST1.BAM~

  COPY ~Silver_Staff_of_Aule\stuff\ER_STA08.ITM~ ~override~ // Aule's Staff
    SAY_EVALUATED IDENTIFIED_DESC ~%er_str0%~
    SAY NAME2 @11
    PATCH_PRINT @115
    PATCH_IF er_eff = 2 BEGIN

      LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~
        INT_VAR match_opcode = 177          // Type: #177 (Use EFF File)
        STR_VAR match_resource = ~FT1DAM~  // Resource: FT1DAM
      END

      LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~
        INT_VAR
          type = 1                    // Melee
          opcode = 177                // Type: #177 (Use EFF File)
          target = 2                  // Target: Preset Target
          parameter1 = 122            // IDS Value: LYCANTHROPE
          parameter2 = 4              // IDS Target: RACE.IDS
          timing = 1                  // Timing Mode: Instant/Permanent Until Death
          probability1 = 100          // Probability1: 100
          insert_point = 0            // First Effect
        STR_VAR resource = ~ER_STDM~  // Resource: ER_STDM
      END
    END

  COPY_EXISTING ~ULGOTH.STO~ ~override~ // Ulgoth's Beard Store and Inn
    PATCH_IF SOURCE_SIZE > 0x9b BEGIN
      ADD_STORE_ITEM ~ER_STA08~ AFTER ~STAF08~ #0 #0 #0 ~IDENTIFIED~ #1
      REMOVE_STORE_ITEM ~STAF08~
    END
  BUT_ONLY_IF_IT_CHANGES

END

BEGIN @104
SUBCOMPONENT @102
REQUIRE_FILE ~override/ER_IST1.BAM~ @107
  COPY ~Silver_Staff_of_Aule\stuff\ER_IST2.BAM~ ~override/ER_IST1.BAM~

BEGIN @105
SUBCOMPONENT @102
REQUIRE_FILE ~override/ER_IST1.BAM~ @107
  COPY ~Silver_Staff_of_Aule\stuff\ER_IST3.BAM~ ~override/ER_IST1.BAM~

BEGIN @103
SUBCOMPONENT @102
REQUIRE_FILE ~override/ER_IST1.BAM~ @107
  COPY ~Silver_Staff_of_Aule\stuff\ER_IST1.BAM~ ~override/ER_IST1.BAM~
